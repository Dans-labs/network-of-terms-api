PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
    skos:prefLabel ?prefLabel ;
    skos:altLabel ?altLabel ;
    skos:scopeNote ?scopeNote .
}
WHERE {
    ?uri a ?type .
    VALUES ?type { schema:Person schema:Organization }
    {
        ?uri schema:name ?prefLabel .
        FILTER(CONTAINS(LCASE(?prefLabel), LCASE(?searchTerm)))
    }
    UNION
    {
        ?uri schema:alternateName ?altLabel .
        FILTER(CONTAINS(LCASE(?altLabel), LCASE(?searchTerm)))
    }
    OPTIONAL { ?uri schema:name ?prefLabel . }
    OPTIONAL { ?uri schema:alternateName ?altLabel . }
    OPTIONAL { ?uri schema:description ?scopeNote . }
}
LIMIT 1000
