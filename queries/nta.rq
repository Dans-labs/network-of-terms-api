CONSTRUCT {
    ?uri a skos:Concept ;
         skos:prefLabel ?prefLabel ;
         skos:altLabel ?altLabel1 ;
         skos:altLabel ?altLabel2 ;
         skos:scopeNote ?scopeNote
}
WHERE {
    GRAPH <http://data.bibliotheken.nl/thesp/> {
        ?uri a schema:Person .
        {
            ?uri rdfs:label ?label .
            ?label <bif:contains> ?searchTerm
        }
        UNION
        {
            ?uri schema:name ?label .
            ?label <bif:contains> ?searchTerm
        }
        UNION
        {
            ?uri schema:alternateName ?label .
            ?label <bif:contains> ?searchTerm
        }
        OPTIONAL { ?uri rdfs:label ?prefLabel }
        OPTIONAL { ?uri rdfs:comment ?scopeNote }
        OPTIONAL { ?uri schema:name ?altLabel1 }
        OPTIONAL { ?uri schema:alternateName ?altLabel2 }
   }
}
LIMIT 1000
